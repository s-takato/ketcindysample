Setwindow([-1.75,1.75],[-1.75,1.75]);
Setunitlen("3cm");
//to move to default positions, delete points
Putpoint("TH",[-5+ 77/20,-3.0],[TH.x,-3.0]); // -5 ~ -5+180/20=4
Putpoint("FI",[-5+ 52/40,-3.5],[FI.x,-3.5]); // -5 ~ -5+360/40=4
//Setangle(77,52); // fixed // view from (sin77cos52,sin77sin52,cos77)
Start3d();
Texparent="";

Addax(0);
Putaxes3d(0);

//Paramplot("xma","[XMAX,t]","t=[YMIN,YMAX]",["dr","Num=50"]);
//Paramplot("xmi","[XMIN,t]","t=[YMIN,YMAX]",["dr","Num=50"]);
//Paramplot("yma","[t,YMAX]","t=[XMIN,XMAX]",["dr","Num=50"]);
//Paramplot("ymi","[t,YMIN]","t=[XMIN,XMAX]",["dr","Num=50"]);
Defvar("cosTH",cos((TH.x+5)*1/9*pi)); Defvar("cosFI",cos((FI.x+5)*2/9*pi));
Defvar("sinTH",sin((TH.x+5)*1/9*pi)); Defvar("sinFI",sin((FI.x+5)*2/9*pi));
Defvar("tanTH",tan((TH.x+5)*1/9*pi)); Defvar("tanFI",tan((FI.x+5)*2/9*pi));
Defvar("lo",[ cosTH*cosFI,cosTH*sinFI,-sinTH]);
Defvar("mo",[-      sinFI,      cosFI,   0  ]);
Defvar("no",[ sinTH*cosFI,sinTH*sinFI, cosTH]);
Deffun("xyz2xy(VVV)",["regional(WWW)","WWW=[VVV.x,VVV.y]","WWW"]);
 Deffun("matvec2vec(MMM,VVV)",["regional(WWW)","
WWW=Op(1,transpose(MMM*transpose([VVV])))
","WWW"]                      );
 Deffun("mat3adj(MMM)",["regional(NNN)","
NNN=[[ Op(2,Op(2,MMM))*Op(3,Op(3,MMM))-Op(3,Op(2,MMM))*Op(2,Op(3,MMM)),
      -Op(2,Op(1,MMM))*Op(3,Op(3,MMM))+Op(3,Op(1,MMM))*Op(2,Op(3,MMM)),
       Op(2,Op(1,MMM))*Op(3,Op(2,MMM))-Op(3,Op(1,MMM))*Op(2,Op(2,MMM)) ],
     [-Op(1,Op(2,MMM))*Op(3,Op(3,MMM))+Op(3,Op(2,MMM))*Op(1,Op(3,MMM)),
       Op(1,Op(1,MMM))*Op(3,Op(3,MMM))-Op(3,Op(1,MMM))*Op(1,Op(3,MMM)),
      -Op(1,Op(1,MMM))*Op(3,Op(2,MMM))+Op(3,Op(1,MMM))*Op(1,Op(2,MMM)) ],
     [ Op(1,Op(2,MMM))*Op(2,Op(3,MMM))-Op(2,Op(2,MMM))*Op(1,Op(3,MMM)),
      -Op(1,Op(1,MMM))*Op(2,Op(3,MMM))+Op(2,Op(1,MMM))*Op(1,Op(3,MMM)),
       Op(1,Op(1,MMM))*Op(2,Op(2,MMM))-Op(2,Op(1,MMM))*Op(1,Op(2,MMM)) ] ]
","NNN"]               );
 Deffun("mat3det(MMM)",["regional(XXX)","
XXX= Op(1,Op(1,mat3adj(MMM)))*Op(1,Op(1,MMM))
    +Op(2,Op(1,mat3adj(MMM)))*Op(1,Op(2,MMM))
    +Op(3,Op(1,mat3adj(MMM)))*Op(1,Op(3,MMM))
","XXX"]               );
 Deffun("mat3inv(MMM)",["regional(LLL)","
LLL=mat3adj(MMM)/mat3det(MMM)
","LLL"]               );
// T.(W-T)=U.(W-U)=V.(W-V)=0
// W=(T,U,V)((T,U,V)^t.(T,U,V))^{-1}.(|T|^2,|U|^2,|V|^2)^t
//  =(T,U,V)^t^{-1}.(|T|^2,|U|^2,|V|^2)^t
 Deffun("perp(TTT,UUU,VVV)",["regional(WWW)","
WWW=matvec2vec(mat3inv([TTT,UUU,VVV]),[Norm(TTT)^2,Norm(UUU)^2,Norm(VVV)^2]);
","WWW"]                    );

//Xyzax3data("","x=[-5,5]","y=[-5,5]","z=[-5,5]");

Defvar("t43=[-1  ,-1  ,-1  ]");
Defvar("t24=[-1  ,-1  , 1  ]");
Defvar("t14=[-1  , 1  ,-1  ]");
Defvar("t40=[-1  , 1  , 1  ]");
Defvar("t04=[ 1  ,-1  ,-1  ]");
Defvar("t41=[ 1  ,-1  , 1  ]");
Defvar("t42=[ 1  , 1  ,-1  ]");
Defvar("t34=[ 1  , 1  , 1  ]");

Defvar("s4=[
[[ 1, 0, 0],[ 0, 0,-1],[ 0, 1, 0]], // [ 1, 0, 0]
[[ 1, 0, 0],[ 0,-1, 0],[ 0, 0,-1]], // [ 1, 0, 0]  2
[[ 1, 0, 0],[ 0, 0, 1],[ 0,-1, 0]], // [ 1, 0, 0]
[[ 0, 0, 1],[ 0, 1, 0],[-1, 0, 0]], // [ 0, 1, 0]
[[-1, 0, 0],[ 0, 1, 0],[ 0, 0,-1]], // [ 0, 1, 0]  5
[[ 0, 0,-1],[ 0, 1, 0],[ 1, 0, 0]], // [ 0, 1, 0]
[[ 0,-1, 0],[ 1, 0, 0],[ 0, 0, 1]], // [ 0, 0, 1]
[[-1, 0, 0],[ 0,-1, 0],[ 0, 0, 1]], // [ 0, 0, 1]  8
[[ 0, 1, 0],[-1, 0, 0],[ 0, 0, 1]], // [ 0, 0, 1]
//
[[ 0, 1, 0],[ 1, 0, 0],[ 0, 0,-1]], // [ 1, 1, 0]
[[ 0,-1, 0],[-1, 0, 0],[ 0, 0,-1]], // [ 1,-1, 0]
[[-1, 0, 0],[ 0, 0, 1],[ 0, 1, 0]], // [ 0, 1, 1]
[[-1, 0, 0],[ 0, 0,-1],[ 0,-1, 0]], // [ 0, 1,-1]
[[ 0, 0, 1],[ 0,-1, 0],[ 1, 0, 0]], // [ 1, 0, 1]
[[ 0, 0,-1],[ 0,-1, 0],[-1, 0, 0]], // [-1, 0, 1]
//
[[ 0, 0, 1],[ 1, 0, 0],[ 0, 1, 0]], // [ 1, 1, 1] 16
[[ 0, 1, 0],[ 0, 0, 1],[ 1, 0, 0]], // [ 1, 1, 1] 17
[[ 0,-1, 0],[ 0, 0, 1],[-1, 0, 0]], // [-1, 1, 1] 18
[[ 0, 0,-1],[-1, 0, 0],[ 0, 1, 0]], // [-1, 1, 1] 19
[[ 0, 0,-1],[ 1, 0, 0],[ 0,-1, 0]], // [ 1, 1,-1] 20
[[ 0, 1, 0],[ 0, 0,-1],[-1, 0, 0]], // [ 1, 1,-1] 21
[[ 0,-1, 0],[ 0, 0,-1],[ 1, 0, 0]], // [ 1,-1, 1] 22
[[ 0, 0, 1],[-1, 0, 0],[ 0,-1, 0]], // [ 1,-1, 1] 23
//
[[ 1, 0, 0],[ 0, 1, 0],[ 0, 0, 1]]  //            24
           ]");
Defvar("a4=[  Op(2,s4),Op(5,s4),Op(8,s4),
Op(16,s4),Op(17,s4),Op(18,s4),Op(19,s4),Op(20,s4),Op(21,s4),Op(22,s4),Op(23,s4),
Op(24,s4)  ]");

Defvar("rU04=t34*1/3+t04*2/3");
Defvar("rl34=t34*2/3+t04*1/3");
Defvar("rU34=t34*2/3+t24*1/3");
Defvar("sU34=t34*2/3+t14*1/3");
Defvar("vA=perp(rU04,rl34,rU34)");
Defvar("vB=perp(sU34,rl34,rU04)");
Defvar("vC=perp(rU34,rl34,sU34)");

forall(1..12,
Spaceline(text(#*6-5),[matvec2vec(Op(#,a4),rl34),
                       matvec2vec(Op(#,a4),(sU34+rl34)/2)],["dr","nodisp"]);
Spaceline(text(#*6-4),[matvec2vec(Op(#,a4),rl34),
                       matvec2vec(Op(#,a4),(rU34+rl34)/2)],["dr","nodisp"]);
Spaceline(text(#*6-3),[matvec2vec(Op(#,a4),rl34),
                       matvec2vec(Op(#,a4),(rU04+rl34)/2)],["dr","nodisp"]);
Spaceline(text(#*6-2),[matvec2vec(Op(#,a4),vA),
                       matvec2vec(Op(#,a4),(vB+vA)/2)],["dr","nodisp"]);
Spaceline(text(#*6-1),[matvec2vec(Op(#,a4),vB),
                       matvec2vec(Op(#,a4),(vC+vB)/2)],["dr","nodisp"]);
Spaceline(text(#*6  ),[matvec2vec(Op(#,a4),vC),
                       matvec2vec(Op(#,a4),(vA+vC)/2)],["dr","nodisp"]);
      );

//Expr([xyz2xy(Projcoordpara([0,0,0])),"c", "0"]);

Ptseg3data();

edges=[];
forall(1..72,
edges=append(edges,"sl3d"+text(#));
      );

if(Isangle(), //if TH/FI is selected
   ,
forall(1..72,
if(mod(#-1,6)<3,
Skeletonparadata(text(#),["sl3d"+text(#)],edges,[.2,"dr"]),
Skeletonparadata(text(#),["sl3d"+text(#)],edges,[.2,"dr,2"])
  );
      );
  );
