//ketlib
use("KetCindyPlugin");
Dircdy=loaddirectory;
setdirectory(plugindirectory);
import("ketcindy.ini");

Initglist();

Setglist();

Setplaybuttons([-5,-5],16,[1]);//no ketjs on
Movetojs(Text61,[2,-5],16);
Movetojs(Text60,[3.5,-5],16);
Movetojs(Text65,[-5.8,4.5],15);
Movetojs(Text66,[1.2,4.5],15);
Movetojs(Text67,[1.2,3.6],15);//no ketjs off

cmdL=[ //no ketjs on
 "assume(a>0,b>0)",[],
 "declare([u1,v1,x1,y1],real)",[],
 "f:x^2/a^2+y^2/b^2-1",[],
 "eq3:ev(f=0,[x=x1+u1*t,y=y1+v1*t])",[],
 "ans:solve(eq3,t)",[],
 "t1:ev(t,ans[1])",[],
 "t2:ev(t,ans[2])",[],
 "dif:ratsimp(t2-t1)",[],
 "t:if dif>=0 then t2 else t1",[],
 "x2:x1+u1*t",[],
 "y2:y1+v1*t",[],
 "t::x2::y2",[]
];
CalcbyM("ans0",cmdL,[""]);

cmdL=[
 "assume(a>0,b>0)",[],
 "declare([u1,v1,x1,y1],real)",[],
 "f:x^2/a^2+y^2/b^2-1",[],
 "n:[diff(f,x),diff(f,y)]",[],
 "n:ev(n,[x=x1,y=y1])",[],
 "n1:n[1]",[],
 "n2:n[2]",[],
 "eq1:n1*(X-x)+n2*(Y-y)=0",[],
 "eq2:-n2*((x+X)/2-x1)+n1*((y+Y)/2-y1)=0",[],
 "eq1:ev(eq1,[x=x1-u1,y=y1-v1])",[],
 "eq2:ev(eq2,[x=x1-u1,y=y1-v1])",[],
 "res:solve([eq1,eq2],[X,Y])",[],
 "u2:ev(X-x1,res[1])",[],
 "v2:ev(Y-y1,res[1])",[],
 "u2:ratsimp(u2)",[],
 "v2:ratsimp(v2)",[],
 "eq3:ev(f=0,[x=x1+u2*t,y=y1+v2*t])",[],
 "ans:solve(eq3,t)",[],
 "ta:ev(t,ans[1])",[],
 "tb:ev(t,ans[2])",[],
 "x2a:x1+u2*ta",[],
 "y2a:y1+v2*ta",[],
 "x2b:x1+u2*tb",[],
 "y2b:y1+v2*tb",[],
 "u2::v2::tb::x2b::y2b::ta::x2a::y2a",[]
];
CalcbyM("ans",cmdL,[""]);

Ketcindyjsdata(["ans0",ans0,"ans",ans]);//no ketjs off

Deffun("fans0(a,b,u1,v1,x1,y1)",[
  "regional(t0,x2,y2)",
  "t0="+ans0_1,"x2="+ans0_2,"y2="+ans0_3,
  "[t0,x2,y2]"
]);
Deffun("fans(a,b,u1,v1,x1,y1)",[
  "regional(u2,v2,t,x2,y2,ta,x2a,y2a)",
  "u2="+ans_1,"v2="+ans_2,
  "t="+ans_3,"x2="+ans_4,"y2="+ans_5,
  "ta="+ans_6,"x2a="+ans_7,"y2a="+ans_8,
  "[u2,v2,t,x2,y2,ta,x2a,y2a]"
]);

aborg=[4,3];
Axorg=3;
A.x=Axorg;
vorg=[-1,1]/sqrt(2);
initflg=1;
pflg=0;
pL=[];

//figures
Ketinit();
Addglist();
Setketcindyjs(["Sc=1.5","No=all","Fig=y","Ax=n","Color=offwhite"]);//no ketjs
Setwindow([-6,6],[-5.5,5]);
Slider("T",[-6,-4],[6,-4]);
Addax(0);
A.y=0;
a=4;b=3;

Cindyparse(str,value):=(
  regional(semi,tmp,tmp1);
  semi=Strsplit(str,";");
  forall(semi,
    if(length(#)>0,
      if(length(#)>0,
        tmp=Strsplit(#,"=");
        if(length(tmp_2)==0,
          tmp1=str+format(value,6);
        ,
          tmp1=tmp_1+"="+Tocindyform(tmp_2);
        );
        parse(tmp1+";");
      );
    );
  );
);

str0="A.x=3";// no ketjs on
str1="vx=-1";
str2="vy=1";// no ketjs off
//str0=Textedit(65);// only ketjs on
//str1=Textedit(66);
//str2=Textedit(67);// only ketjs off
Cindyparse(str0,A.x);
Ptpos(A,A.xy);
pA=Ptpos(A);
Cindyparse(str1,-1);
Cindyparse(str2,1);

vec=[vx,vy];
vec=vec/|vec|;
Arrowdata("1",[pA,pA+vec],["dr,2","Color=red","border->false"]);

Eps=10^(-4);
if((|pA_1-Axorg|>Eps)%(|vec-vorg|>Eps)%(|[a,b]-aborg|>Eps),
  vorg=vec;
  Axorg=pA;
  aborg=[a,b];
  initflg=1;
);

pF=[-sqrt(a^2-b^2),0];
C.xy=pF;
Drwxy();
fun=Assign("[a*cos(t),b*sin(t)]",["a",a,"b",b]);
Paramplot("1",fun,"t=[0,2*pi]");
Pointdata("1",C,["Size=3","Color=red","border->false"]);

if(initflg==1,
  tt=200;
  u1=vec_1; v1=vec_2; x1=A.x; y1=0;
  tmp=fans0(a,b,u1,v1,x1,y1);
  t0=tmp_1; x2=tmp_2; y2=tmp_3;
  pL=[[0,[x1,y1],[u1,v1]],[t0,[x2,y2],[u1,v1]]];
  ts=t0;
  while(ts<tt,
    x1=x2; y1=y2;
    tmp=fans(a,b,u1,v1,x1,y1);
    u2=tmp_1; v2=tmp_2; t2=tmp_3; x2=tmp_4; y2=tmp_5;
    if(abs(t2)<Eps,
      t2=t2a; x2=x2a; y2=y2a;
    );
    ts=ts+t2;
    pL=append(pL,[ts,[x2,y2],[u2,v2]]);
    u1=u2; v1=v2;
  );
  initflg=0;
);

if(pflg==1,
  tt=ts+3000;
  tmp=pL_(length(pL));
  ts=tmp_1;
  x1=tmp_2_1;y1=tmp_2_2;u1=tmp_3_1;v1=tmp_3_2;
  forall(1..1000,
    if(ts>tt,pflg=0);
    if(pflg==1,
      tmp=fans(a,b,u1,v1,x1,y1);
      u2=tmp_1; v2=tmp_2; t2=tmp_3; x2=tmp_4; y2=tmp_5;
      if(abs(t2)<Eps,
        t2=tmp_6; x2=tmp_7; y2=tmp_8;
      );
      ts=ts+t2;
      pL=append(pL,[ts,[x2,y2],[u2,v2]]);
      u1=u2; v1=v2; x1=x2; y1=y2;
    );
  );
  pflg=0;
);

mf(ss):=(
  regional(plist,tmp,tmp1,tmp2);
  plist=select(pL,#_1<=ss);
  tmp=length(plist);
  tmp1=plist_tmp;
  if(tmp1_1<ss,
    tmp2=pL_(tmp+1);
    tmp=tmp1_2+(tmp2_2-tmp1_2)/(tmp2_1-tmp1_1)*(ss-tmp1_1);
    plist=append(plist,[ss,tmp]);
  );
  plist=apply(plist,#_2);
  if(length(plist)>1,
    Listplot("1",plist);
  );
);

ss=Animationparam(0,2^(T.x+6),[0,tmp_1]);
mf(ss);

Letter([-4.5,3.3],"nw",text(re(floor(ss))),["Size=1.2"]);
tmp=pL_(length(pL));
Letter([-4.5,2.5],"nw",text(re(floor(tmp_1))),["Size=1.2"]);

Windispg();
