//ketlib
tmp1="[P,Q,F,G]";
tmp2="[Q,R/(I0+(m1+m2)*R^2)*(g*(m2-m1*cos(F))-m1*(L0+R*(F-P))*G^2),G,1/(L0+R*(F-P))*(2*R*Q*G-g*sin(F)-R*G^2)]";
eqs=Assign(tmp2,["F","Phi","G","DPhi","P","Psi","Q","DPsi"]);
eq=tmp1+"'="+tmp2;

//figures
Ketinit();
Setketcindyjs(["No=all","Local=n"]);//no ketjs
Setparent(Cdyname()+"fig");
Setax(["a"]);
Setwindow([-6,6],[-5,2]);
Current=0;

Slider("L",[3,-5],[5,-5]);
repnm=["R","I0","m1","m2","g","L0"];
repva=[1,0.05,1,1.05,9.8,L.x];

rep=apply(1..(length(repnm)),[repnm_#,repva_#]);
rep=flatten(rep);
Defvar(rep);

eqn=Assign(eq,rep);
vL=Deqdata(eqn,"t=[0,60]",0,[0,0,0,0.6],300);



p0=[0,0];
Circledata("1",[p0,1]);
Shade("1",["cr1"],["Color=[0,0,0,0.2]"]);

mf(s):=(
 regional(tmp,phi,psi,p1,p2,q1,q2); 
 tmp=vL_(s+1);
 phi=tmp_4;
 psi=tmp_2;
 p1=R*[cos(phi),sin(phi)];
 p2=p1+(L0+R*phi)*[sin(phi),-cos(phi)];
 q1=R*[cos(pi-psi),sin(pi-psi)];
 q2=q1+(L0+R*psi)*[-sin(pi-psi),cos(pi-psi)];
 Listplot("1",[p1,p2],["dr,2"]);
 Listplot("2",[q1,q2],["dr,2"]);
 Partcrv("1",p1,q1,"cr1",["dr,2"]);
 Pointdata("1",[p2,q2],["Size=8","Color=red"]);
 Expr([1,1.5],"e","t="+Sprintf(s/10,2),["Size=1.5"]);
);

Setpara("atwood","mf(s)","s=[0,30]",["Div=25"],
 ["Frate=10","Scale=1","OpA=","Title=Cycloid"]);

if(isreal(Animeflg),
  if(Animeflg==1,
    sstart=Dirflg*10*seconds()+sorg;
  );
,
  sstart=Current;
);
if(sstart>=300,
 sstart=300;
 stopanimation();
);

mf(sstart);

Figpdf();
Windispg();
