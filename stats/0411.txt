use("KetCindyPlugin");
Dircdy=loaddirectory;
setdirectory(gethome());
import("ketcindy.ini");

px=-3.5; py=[3.6,3,2.4,1.8];
Seteditable(3,["","Size=20","Width=90"]); //no ketjs on
Seteditable(4,["","Size=20","Width=90"]);
Seteditable(5,["","Size=20","Width=90"]);
Seteditable(6,["","Size=20","Width=90"]);
Text3.xy=[px,py_1];
Text4.xy=[px,py_2];
Text5.xy=[px,py_3];
Text6.xy=[px,py_4];
Text7.xy=[px-1.6,py_2];
Text8.xy=[px-1.6,py_3];
Text9.xy=[px-1.6,py_4]; //no ketjs off
px=px-1.8; py=apply(py,#+0.1);

x995=[
7.88,10.6,12.84,14.86,16.75,18.55,20.28,21.95,23.59,25.19,
26.76,28.3,29.82,31.32,32.8,34.27,35.72,37.16,38.58,40,
41.4,42.8,44.18,45.56,46.93,48.29,49.64,50.99,52.34,53.67,
55,56.33,57.65,58.96,60.27,61.58,62.88,64.18,65.48,66.77,
68.05,69.34,70.62,71.89,73.17,74.44,75.7,76.97,78.23,79.49,
80.75,82,83.25,84.5,85.75,86.99,88.24,89.48,90.72,91.95
]; //1--60

gammacoef=[676.5203681218851 , - 1259.1392167224028 ,771.32342877765313 , - 176.61502916214059 , 12.507343278686905 ,- 0.13857109526572012 , 9.9843695780195716*10^(-6),1.5056327351493116 * 10^(- 7)];

gamma(z0):=(
  regional(z,x,y,t);
  z = z0;
  if(z<0.5,y=pi/(sin(pi*z)*gamma(1-z)),
    z = z-1;
    x = 0.99999999999980993;
    repeat(length(gammacoef),x=x+(gammacoef_#)/(z+#));
    t = z + length(gammacoef)-0.5;
    y = sqrt(2*pi)*t^(z+0.5)*exp(-t)*x;
  );
  y;
);

gammp(a,x):=(
  regional(gamser,gammcf,rtn);
  if((x < 0.0)%(a<=0.0),println("Invalid arguments in routine gammp"),
    if(x<(a+1.0),gamser=gser(a,x);rtn=gamser,gammcf=gcf(a,x);rtn=1.0-gammcf));
  rtn
);

gammq(a,x):=(
  regional(gamser,gammcf,rtn);
  if((x < 0.0)%(a<=0.0),println("Invalid arguments in routine gammq"),
    if(x<(a+1.0),gamser=gser(a,x);rtn=1.0-gamser,gammcf=gcf(a,x);rtn=gammcf));
  rtn
);

gser(a,x):=(
  regional(n,sum,del,ap,gln,gamser,loop,itmax,eps);
  itmax = 100;
  eps = 0.0000001;
  loop=1;
  gln=log(gamma(a));
  if (x<=0.0, if(x<0.0, println("x less than 0 in routine gser"),gamser=0.0),
    ap = a;
    sum = 1.0/a;
    del = sum;
    n = 1;
    while(loop>0,
      ap = ap+1;
      del = del*(x/ap);
      sum = sum + del;
      if (abs(del)<abs(sum)*eps,gamser=sum*exp(-x+a*log(x)-gln);loop=-1);
      if(n>=itmax,println("a too large, itmax too small in routine gser");loop=-1,n=n+1)
    )
  );
  gamser
);

gcf(a,x):=(
  regional(i,an,b,c,d,del,h,gammcf,gln,loop,itmax,eps,fpmin);
  itmax = 100;
  eps = 0.0000001;
  fpmin = 0.000000000000000000000000000001;
  loop=1;
  gln=log(gamma(a));
  b=x+1.0-a;
  c=1.0/fpmin;
  d=1.0/b;
  h=d;
  i=1;
  while(loop>0,
    an=-i*(i-a);
    b=b+2.0;
    d=an*d+b;
    if(abs(d)<fpmin,d=fpmin);
    c=b+an/c;
    if(abs(c)<fpmin,c=fpmin);
    d=1.0/d;
    del=d*c;
    h=h*del;
    if (abs(del-1.0)<eps,loop=-1);
    if(i>=itmax,println("a too large, itmax too small in routine gcf");loop=-1,i=i+1)
  );
  gammcf=exp(-x+a*log(x)-gln)*h;
);

incgamm(a,x):=gammq(a,x)*gamma(a);

dchi(x,n):=1/(2^(n/2)*gamma(n/2))*x^(n/2-1)*exp(-x/2);
pchi(x,n):=1-gammq(n/2,x/2);

qchi(p,n):=(
  regional(eps,x1,x2,x,y1,y2,ctr);
  eps=10^(-6);
  x1=0; x2=300;
  y1=1-pchi(x1,n);
  y2=1-pchi(x2,n);
  ctr=1;
  while((|y2-y1|>eps)&(ctr<40),
    x=(x1+x2)/2;
    y=1-pchi(x,n);
    if(y>p,
      x1=x; y1=y;
    ,
      x2=x; y2=y;
    );
    ctr=ctr+1;
  );
  x;
);

prn(val):=println(format(val,6));//no ketjs

dfnow=0;
axnow=A.x;
ttnow=axnow;
choice=1;
sc=1;


/////////////////////////////


Ketinit();

Setketcindyjs(["Size=1.1"]);
//Ketcindyjsmain(//no ketjs
//["<pf5/fp>_;$t$分布の$p$値","<pf4/fp>_;_;$df$は自由度"],//no ketjs
//["<p><small>&copy;2021 Setsuo Takato</small></p>"]);//no ketjs
Ketcindyjsbody([],//no ketjs on
["<footer>","<p><small>_;&copy; 2021 Setsuo Takato</small></p>","</footer>"]);
// no ketjs off

Addax(0);
xmn=0; xmx=8;
Setscaling(4);
Setwindow([xmn,xmx],[-0.1,1.1]);
Setax(["","x","","",""," "]);
drwxy();
Slider("A",[XMIN,0],[XMAX-0.00001,0]);
inspect(A,"ptsize",5.1);// no ketjs

op=["dr,2"];
opb=["dr,2","Color=blue"];
opr=["dr,2","Color=red"];
opg=["dr,2","Color=[0.64,0,0.95,0.4]"];

tmp=Textedit(3,"3","");
if(length(tmp)>0,
  df=parse(tmp);
,
  df=dfnow;
  Subsedit(3,text(df));
);

if(df!=dfnow,
  sc=ceil(x995_df/XMAX);
  tmp1=apply(0..100,[#/100,dchi(#/100*sc,df)]);
  tmp2=apply(0..50,
    [1+#/50*(XMAX-1),dchi((1+#/50*(XMAX-1))*sc,df)]);
  fun1=concat(tmp1,tmp2);
  tt=Textedit(4,"");
  tt=parse(tt);
  ttnow=tt;
  Putpoint("A",[tt/sc,0]);
  dfnow=df;
);

Subsedit(3,text(dfnow));

op=["dr,2","Num=150"];
opr=append(op,"Color=red");
opb=append(op,"Color=blue");
opg=append(op,"Color=[0.64,0,0.95,0.4]");
Listplot("-gr1",fun1,opb);
//Listplot("-gr2",fun2,opg);
//Listplot("-gr3",fun3,op);
//Listplot("h1",[[XMIN,1],[XMAX,1]]);

Optc=["Color=red","Size=2"];
Expr([px,py_(choice+1)/SCALEY],"w","\bullet",Optc);
tmp=px+1;
Expr([tmp,py_1/SCALEY],"e","df=",["Size=1.3"]);
Expr([tmp,py_2/SCALEY],"e","x=",["Size=1.3"]);
Expr([tmp,py_3/SCALEY],"e","pr=",["Size=1.3"]);
Expr([tmp,py_4/SCALEY],"e","pl=",["Size=1.3"]);


if(choice==1,
  tmp=Textedit(4,"3");
  tt=parse(tmp);
  if(tt!=ttnow,
    if((tt/sc-XMIN)*(tt/sc-XMAX)<0,
      Putpoint("A",[tt/sc,0]);
      ttnow=tt;
      axnow=tt;
    );
  );
);
if(choice==2,
  tmp=Textedit(5,"","");
  if(length(tmp)>0,
    pp=parse(tmp);
  ,
    pp=1-pchi(ttnow,df);
    Subsedit(5,Sprintf(pp,4));
  );
  tt=qchi(pp,df);
  Putpoint("A",[tt/sc,0]);
  axnow=tt;
  Subsedit(4,Sprintf(tt,4));
  Subsedit(6,format(1-pp,4));
);

if(choice==3,
  tmp=Textedit(6,"","");
  if(length(tmp)>0,
    pp=parse(tmp);
  ,
    pp=pchi(ttnow,df);
    Subsedit(6,Sprintf(pp,4));
  );
  eps=10^(-6);
  x1=XMIN; x2=XMAX;
  y1=pchi(x1,dfnow);
  y2=pchi(x2,dfnow);
  ctr=1;
  while((|y2-y1|>eps)&(ctr<20),
    x=(x1+x2)/2;
    y=pchi(x,dfnow);
    if(y<pp,
      x1=x; y1=y;
    ,
      x2=x; y2=y;
    );
    ctr=ctr+1;
  );
  tt=x;
  Putpoint("A",[tt/sc,0]);
  axnow=tt;
  Subsedit(4,Sprintf(tt,4));
  Subsedit(5,format(1-pp,4));
);

tmp=pchi(sc*A.x,df);
//pQ=[A.x,tmp];
pR=[A.x,1-tmp];
//Listplot("1",[A,pQ],["Color=red"]);
//Listplot("2b",[A,pR],["Color=red"]);
//Listplot("3",[pQ,[0,pQ_2]],
//   ["do,1.5,2","Color=[0.64,0,0.95,0.4]"]);
//Listplot("4",[pR,[0,pR_2]],["do,1.5,2"]);
Htickmark([A.x,Sprintf(sc*A.x,4),XMAX,"s2",text(sc*XMAX)]);
//Vtickmark([pQ_2,Sprintf(pQ_2,2),
//    pR_2,Sprintf(pR_2,2),1,"nw1","1"]);
Vtickmark([0.5,"0.5",1,"nw1","1"]);

pP=[A.x,dchi(sc*A.x,dfnow)];
Listplot("1",[pP,[A.x,0]],opr);
//Partcrv("1",pP,pM,"gr1",["nodisp"]);
//Joincrvs("1",["sg1","part1"],["nodisp"]);
//Shade(["join1"],["Color=cyan"]);

if(choice==1,
  Subsedit(4,Sprintf(sc*A.x,4));
  tmp=Sprintf(pR_2,4);
  Subsedit(5,tmp);
  tmp=Sprintf(1-pR_2,4);
  Subsedit(6,tmp);
);

Windispg();

//Help("Textedi");di");